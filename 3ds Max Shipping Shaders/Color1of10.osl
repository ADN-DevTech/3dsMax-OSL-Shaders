// Pick one of ten inputs
// Color1of10.osl, by Zap Andersson
// Modified: 2018-12-11
// Copyright 2018 Autodesk Inc, All rights reserved. This file is licensed under Apache 2.0 license
//    https://github.com/ADN-DevTech/3dsMax-OSL-Shaders/blob/master/LICENSE.txt

shader Color1of10
[[ string help = "Pick one of 10 inputs",
   string label= "1 of 10 (color)",
   string category = "Switchers" ]]
(
	int   Index    = 0 
		[[ string help  = "Pick the index (number) of the input to use. "]],
	color I0     = color(1.0, 0.5, 0.4) [[ string label = "Input 0" ]],
	color I1     = color(1.0, 1.0, 0.4) [[ string label = "Input 1" ]],
	color I2     = color(0.5, 1.0, 0.4) [[ string label = "Input 2" ]],
	color I3     = color(0.5, 1.0, 1.0) [[ string label = "Input 3" ]],
	color I4     = color(0.0, 0.5, 1.0) [[ string label = "Input 4" ]],
	color I5     = color(1.0, 0.5, 1.0) [[ string label = "Input 5" ]],
	color I6     = color(1.0, 1.0, 1.0) [[ string label = "Input 6" ]],
	color I7     = color(0.6, 0.6, 0.6) [[ string label = "Input 7" ]],
	color I8     = color(0.2, 0.2, 0.2) [[ string label = "Input 8" ]],
	color I9     = color(0.1, 0.1, 0.1) [[ string label = "Input 9" ]],

	int   StartIdx = 0 
		[[ string label = "Start Index",
		   string help  = 
				"Offsets the input index so that this number will map to the first input. "
				"E.g. if set to 10, the first input (labeled Input 0) will map to index 10, rather than 0. "
				"This is used when cascading multiple switchers into each other and allows them "
				"to cover different ranges." ]],
	int   Mod    = 0 
		[[ string label = "Range: Number of inputs", 
		   string help  = "If 0, returns all outputs and any outside the range uses the 'Outside Range' input. "
		                  "If a nonzero number, only uses that many inputs, repeated in a rotating fashion." ]],
	color Outside = 0.0 
		[[ string label = "Outside Range",
		   string help  = 
				"The value to return when the index falls outside the set of available inputs. "
				"This can be used to connect a second switcher to support a larger number of inputs. "]],
	
	output color Out = color(0.1,0.2,0.3),
)
{
	int idx = Index - StartIdx;
	
	if (Mod) 
		idx = idx % Mod;

	if (idx < 0 || idx > 9)
	{
		Out = Outside;
	}
	else
	{
#define OUT(X) if (idx == X) { Out = I##X; } else
		OUT(0)
		OUT(1)
		OUT(2)
		OUT(3)
		OUT(4)
		OUT(5)
		OUT(6)
		OUT(7)
		OUT(8)
		OUT(9)
		Out = Outside;
#undef OUT
	}
}
