// Pick one of ten inputs
// Float1of10.osl, by Zap Andersson
// Modified: 2018-02-08
// Copyright 2018 Autodesk Inc, All rights reserved. This file is licensed under Apache 2.0 license
//    https://github.com/ADN-DevTech/3dsMax-OSL-Shaders/blob/master/LICENSE.txt

shader Float1of10
[[ string help = "Pick one of 10 inputs",
   string label= "1 of 10 (float)",
   string category = "Switchers" ]]
( 
	int   Index    = 0 
		[[ string label = "Input index to use",
		   string help  = "Pick the index (number) of the input to use. "]],
	float I0     = 0.0 [[ string label = "Input 0" ]],
	float I1     = 0.0 [[ string label = "Input 1" ]],
	float I2     = 0.0 [[ string label = "Input 2" ]],
	float I3     = 0.0 [[ string label = "Input 3" ]],
	float I4     = 0.0 [[ string label = "Input 4" ]],
	float I5     = 0.0 [[ string label = "Input 5" ]],
	float I6     = 0.0 [[ string label = "Input 6" ]],
	float I7     = 0.0 [[ string label = "Input 7" ]],
	float I8     = 0.0 [[ string label = "Input 8" ]],
	float I9     = 0.0 [[ string label = "Input 9" ]],
	int   StartIdx = 0 
		[[ string label = "Start Index",
		   string help  = 
				"Offsets the input index so that this number will map to the first input. "
				"E.g. if set to 10, the first input (labeled Input 0) will map to index 10, rather than 0. "
				"This is used when cascading multiple switchers into each other and allow them "
				"to cover different ranges." ]],
	int   Mod    = 0 
		[[ string label = "Range: Number of inputs", 
		   string help  = "If 0, returns all outputs and any outside the range uses the 'Outside Range' input. "
		                  "If a nonzero number, only uses that many inputs, repeated in a rotating fashion." ]],
	float Outside = 0.0 
		[[ string label = "Outside Range",
		   string help  = 
				"The value to return when the index falls outside the set of available inputs. "
				"This can be used to connect a second switcher to support a larger number of inputs. "]],
	
	output float Out = 0.0,
)
{
	int idx = Index - StartIdx;
	
	if (Mod) 
		idx = idx % Mod;

	if (idx < 0 || idx > 9)
	{
		Out = Outside;
	}
	else
	{
#define OUT(X) if (idx == X) { Out = I##X; } else
		OUT(0)
		OUT(1)
		OUT(2)
		OUT(3)
		OUT(4)
		OUT(5)
		OUT(6)
		OUT(7)
		OUT(8)
		OUT(9)
		Out = Outside;
#undef OUT
	}
}
