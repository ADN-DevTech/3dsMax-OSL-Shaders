// A simple linear Tri-tone color corrector using Highlights/Midtones/Shadows
// TriTone.osl, by Zap Andersson
// Modified: 2019-11-22
// Copyright 2019 Autodesk Inc, All rights reserved. This file is licensed under Apache 2.0 license
//    https://github.com/ADN-DevTech/3dsMax-OSL-Shaders/blob/master/LICENSE.txt

shader TriTone
[[ string help = "Color corrects an Input color using<br>Highlights/Midtones/Shadows",
   string label= "Tri-tone",
   //string category = "Color Correction" 
]]
(
	color Input    = u
		[[ string label = "Input Color" ]],
	
	color Highlights = 1.0,
	color Midtones   = color(0.3, 0.12, 0.06),
	color Shadows    = 0.0,
	
	float MidtoneLum = 0.2
		[[ string label = "Midtone Luminance" ]],
	
	output color Out = 0.0,
)
{
	color below = clamp((Input / MidtoneLum), 0.0, 1.0);
	color above = clamp((Input - MidtoneLum) / (1.0 - MidtoneLum), 0.0, 1.0);
	Out = mix(Shadows, mix(Midtones, Highlights, above), below);
}
