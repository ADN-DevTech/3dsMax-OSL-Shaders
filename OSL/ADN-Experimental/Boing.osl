// Boing.osl by Zap Andersson
// Inspired by the old Amiga demo, as well as the guy who converted
// the Amiga Juggler demo into full 3d in OSL. This is only 2d, tho...
// Licence: CC0
shader Boing
	[[ string help="Inspired by the clasic Amiga 'Boing' demo<br>"
	               "Apply to a plane in the 3ds Max viewport and<br>"
	               "hit that 'Play' button :)  Enjoy - Zap", 
	   string URL ="https://RenderingTips.com" ]]
(
	float seconds = 0 [[ int timeValue = 1 ]],
	// Inputs
	float  unitsPerSecX = 0.7,
	float  bounceTime   = 1.0, 
	float  radius       = 0.15,
	float  border       = 0.05,
	vector shadowdist   = vector(0.1, 0.0, 0.0),
	int    rez          = 200,
	int    Pixelate     = 1 [[ string widget = "checkBox", string label = "Pixelate to shitty low rez" ]],
	int    Retrofy      = 1 [[ string widget = "checkBox", string label = "Make it really retro looking" ]],
	
	// Outputs
	output color Out = 0.0
)
{
	point pnt = vector(u,v,0);
	
	// Pixelate the whole thing by making UV low-rez
	if (rez > 0 && Pixelate)
	{
		pnt[0] = int(pnt[0] * rez) / float(rez);
		pnt[1] = int(pnt[1] * rez) / float(rez);		
	}

	Out = 0.2;
	
	float edge = border * 2.0;
	float h = (1.0 - edge*2.0);

	// Grid background	
	vector px = (pnt - edge) / h;
	if (px[0] > 0.0 && px[0] < 1.0 && px[1] > -0.1 && px[1] < 1.0)
	{
		if (px[1] < 0)
		{
			px[0] = (px[0] - 0.5) * (1.0+px[1]*2.0) + 0.5;
			px[1] *= 3;
		}
		int ix = int(px[0] * 101);
		int iy = int(px[1] * 101);
		
		if (ix % 10 == 0 | iy % 10 == 0)
			Out = color(0.0, 1.0, 1.0);
	}

	// Boing	
	float p  = seconds * unitsPerSecX;	
	int   pi = (int)floor(p);
	
	float x = 0.0;
	
	if (pi & 1)
		x = p - pi;
	else
		x = 1.0 - (p - pi);
	
	x = x * (1.0 - radius * 2.0 - border * 2.0) + radius + border;
	
	
	float y = abs(sin(seconds / bounceTime * 3.14))  * (1.0 - radius * 2.0 - border * 2.0) + radius + border;
	
	point ballpnt = vector(x, y, 0);

	// Shadow	
	if (distance(pnt, ballpnt + shadowdist) < radius)
		Out *= 0.5;

	// Ball
	vector dist = (pnt - ballpnt);                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                    
	if (length(dist) < radius)
	{
		dist = rotate(dist, 0.3, point(0, 0, 0), point(0, 0, 1));

		float dx = dist[0] / radius;
		float dy = dist[1] / radius;

		dx = dx / sqrt(1.0 - (dy * dy));

		if (sin(dx * 8.0 + seconds * 6.0) > 0.0 ^ sin(dy * 8.0) > 0.0)		
			Out = color(1,0,0);
		else
			Out = color(1,1,1);
	}	
	
	// Bad Old Television Scanlines
	if (Retrofy)
	{
		int scanline = int(v * rez);
		if (scanline % 2 == 0)
			Out *= 0.5 + sin((v + seconds) * 20) * 0.1;
	}
	
}
